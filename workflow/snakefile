from pathlib import Path

# Detect reads
SAMPLES_reads = sorted({f.stem.rsplit("_", 1)[0] for f in Path("reads").glob("*_1.fq.gz")})
SAMPLES_bams = sorted({f.stem.rsplit("_", 1)[0] for f in Path("mapped").glob("*.bam")})

if not SAMPLES_reads and SAMPLES_bams:
    print("Using sequencing reads for the genome skimming")
    SAMPLES=SAMPLES_bams

if not SAMPLES_bams and SAMPLES_reads:
    print("Using bam files for the genome skimming")
    SAMPLES=SAMPLES_reads

if not SAMPLES_bams and not SAMPLES_reads:
    sys.exit(f"âŒ  No input data was provided in the reads or bams directories")

# Generate output
rule all:
    input:
        "distance_matrix.txt",
        "pcoa.png",
        "heatmap.png",
        "depth.tsv"

# Rules
rule bam_to_fastq:
    input:
        "mapped/{sample}.bam"
    output:
        r1="reads/{sample}_1.fq",
        r2="reads/{sample}_2.fq"
    log:
        "logs/bam_to_fastq/{sample}.log"
    threads: 4
    params:
        collate="",
        fastq="-n",
    resources:
        mem_mb=lambda wildcards, input, attempt: max(8*1024, int(input.size_mb) * 2 ** (attempt - 1)),
        runtime=lambda wildcards, input, attempt: max(15, int(input.size_mb / 20) * 2 ** (attempt - 1))
    wrapper:
        "v7.2.0/bio/samtools/fastq/separate"

rule decompress_fastq:
    input:
        r1="reads/{sample}_1.fq.gz",
        r2="reads/{sample}_2.fq.gz"
    output:
        r1="reads/{sample}_1.fq",
        r2="reads/{sample}_2.fq"
    log:
        "logs/decompress_fastq/{sample}.log"
    threads: 1
    localrule: True
    shell:
        """
        gunzip -c {input.r1} > {output.r1}
        gunzip -c {input.r2} > {output.r2}
        """

rule simplify_reads:
    input:
        r1="reads/{sample}_1.fq",
        r2="reads/{sample}_2.fq"
    output:
        "reads/{sample}/{sample}.fq"
    log:
        "logs/decompress_fastq/{sample}.log"
    threads: 1
    localrule: True
    shell:
        """
        rm {input.r2}
        mv {input.r1} {output}
        """

rule skmer_reference:
    input:
        "reads/{sample}/{sample}.fq"
    output:
        "library/{sample}/{sample}.dat"
    log:
        "logs/skmer/{sample}.log"
    threads: 1
    conda:
        "envs/skmer.yml"
    params:
        inputdir="reads/{sample}",
        outputbase="library"
    resources:
        mem_mb=lambda wildcards, input, attempt: max(8*1024, int(input.size_mb * 10) * 2 ** (attempt - 1)),
        runtime=lambda wildcards, input, attempt: max(15, int(input.size_mb / 60) * 2 ** (attempt - 1))
    shell:
        """
        skmer reference {params.inputdir} -p {threads} -l {params.outputbase}
        """

rule skmer_distance:
    input:
        expand("library/{sample}/{sample}.dat", sample=SAMPLES)
    output:
        "distance_matrix.txt"
    log:
        "logs/skmer/distance.log"
    threads: 1
    conda:
        "envs/skmer.yml"
    params:
        inputdir="library",
        outputbase="distance_matrix"
    resources:
        mem_mb=lambda wildcards, input, attempt: max(64*1024, int(input.size_mb * 10) * 2 ** (attempt - 1)),
        runtime=lambda wildcards, input, attempt: max(15, int(input.size_mb / 50) * 2 ** (attempt - 1))
    shell:
        """
        skmer distance {params.inputdir} -p {threads} -o {params.outputbase}
        """

rule seq_depth:
    input:
        expand("reads/{sample}/{sample}.fq", sample=SAMPLES)
    output:
        "depth.tsv"
    log:
        "logs/seq_depth/seq_depth.log"
    threads: 1
    conda:
        "envs/plot.yml"
    resources:
        mem_mb=lambda wildcards, input, attempt: max(64*1024, int(input.size_mb * 10) * 2 ** (attempt - 1)),
        runtime=lambda wildcards, input, attempt: max(15, int(input.size_mb / 50) * 2 ** (attempt - 1))
    shell:
        """
        python workflow/scripts/reads_per_sample.py --out {output} {input}
        """

rule skmer_pcoa:
    input:
        "distance_matrix.txt"
    output:
        "pcoa.png"
    log:
        "logs/skmer/pcoa.log"
    threads: 1
    conda:
        "envs/plot.yml"
    params:
        outputbase="distance_matrix"
    resources:
        mem_mb=lambda wildcards, input, attempt: 4*1024,
        runtime=lambda wildcards, input, attempt: 5
    shell:
        """
        python workflow/scripts/pcoa_from_distance.py --dist {input} --out-prefix pcoa
        """

rule skmer_heatmap:
    input:
        "distance_matrix.txt"
    output:
        "heatmap.png"
    log:
        "logs/plots/heatmap.log"
    threads: 1
    conda:
        "envs/plot.yml"
    params:
        outputbase="distance_matrix"
    resources:
        mem_mb=lambda wildcards, input, attempt: 4*1024,
        runtime=lambda wildcards, input, attempt: 5
    shell:
        """
        python workflow/scripts/heatmap_from_distance.py --dist {input} --out {output}
        """